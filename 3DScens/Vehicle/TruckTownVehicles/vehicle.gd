extends VehicleBody3D
@onready var set_respawn_timer: Timer = $setRespawnTimer

const STEER_SPEED = 1.5
const STEER_LIMIT = 0.4
const BRAKE_STRENGTH = 2.0
#@onready var spawn_point: Node3D = %spawnPoint

@export var engine_force_value := 40.0
var spawnPos:Vector3
var previous_speed := linear_velocity.length()
var _steer_target := 0.0

@onready var desired_engine_pitch: float = $EngineSound.pitch_scale

func _ready() -> void:
	#print("Multiplayer authority will be set to: ", name.to_int())
	#set_multiplayer_authority(name.to_int())
	spawnPos = global_position

func _physics_process(delta: float):
	var fwd_mps := (linear_velocity * transform.basis).x

	_steer_target = Input.get_axis("ui_right", "ui_left")
	_steer_target *= STEER_LIMIT

	# Engine sound simulation (not realistic, as this car script has no notion of gear or engine RPM).
	desired_engine_pitch = 0.05 + linear_velocity.length() / (engine_force_value * 0.5)
	# Change pitch smoothly to avoid abrupt change on collision.
	$EngineSound.pitch_scale = lerpf($EngineSound.pitch_scale, desired_engine_pitch, 0.2)

	if abs(linear_velocity.length() - previous_speed) > 1.0:
		# Sudden velocity change, likely due to a collision. Play an impact sound to give audible feedback,
		# and vibrate for haptic feedback.
		$ImpactSound.play()
		Input.vibrate_handheld(100)
		for joypad in Input.get_connected_joypads():
			Input.start_joy_vibration(joypad, 0.0, 0.5, 0.1)

	# Automatically accelerate when using touch controls (reversing overrides acceleration).
	if DisplayServer.is_touchscreen_available() or Input.is_action_pressed("ui_up"):
		# Increase engine force at low speeds to make the initial acceleration faster.
		var speed := linear_velocity.length()
		if speed < 5.0 and not is_zero_approx(speed):
			engine_force = clampf(engine_force_value * 5.0 / speed, 0.0, 100.0)
		else:
			engine_force = engine_force_value

		if not DisplayServer.is_touchscreen_available():
			# Apply analog throttle factor for more subtle acceleration if not fully holding down the trigger.
			engine_force *= Input.get_action_strength("ui_up")
	else:
		engine_force = 0.0

	if Input.is_action_pressed("ui_down"):
		# Increase engine force at low speeds to make the initial reversing faster.
		var speed := linear_velocity.length()
		if speed < 5.0 and not is_zero_approx(speed):
			engine_force = -clampf(engine_force_value * BRAKE_STRENGTH * 5.0 / speed, 0.0, 100.0)
		else:
			engine_force = -engine_force_value * BRAKE_STRENGTH

		# Apply analog brake factor for more subtle braking if not fully holding down the trigger.
		engine_force *= Input.get_action_strength("ui_down")

	steering = move_toward(steering, _steer_target, STEER_SPEED * delta)

	previous_speed = linear_velocity.length()


func _input(event: InputEvent) -> void:
	if event.is_action("ui_accept"):
		respawn()

func respawn(respawnTo:Vector3 = Vector3(0,0,0)):
	global_position = spawnPos if respawnTo == Vector3(0,0,0) else respawnTo


func _on_set_respawn_timer_timeout() -> void:
	#print("timer ", spawnPos)
	for c in get_children():
		if c is VehicleWheel3D:
			#print(c)
			if !c.is_in_contact():
				set_respawn_timer.start(5)
				return
	spawnPos = global_position
	set_respawn_timer.start(5)
